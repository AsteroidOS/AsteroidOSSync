plugins {
    id 'com.android.library'
    id 'digital.wup.android-maven-publish' version '3.6.2'
    id 'signing'
}

group = 'io.github.dreierf'
version = '0.0.6'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {}
        release {}
    }

    lintOptions {
        abortOnError false
    }

    dependencies {
        implementation 'androidx.appcompat:appcompat:1.0.2'
        implementation 'com.google.android.material:material:1.0.0'

        androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    }
}

task generateJavadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    classpath += project.files("${project.android.sdkDirectory}/platforms/${project.android.compileSdkVersion}/android.jar")

    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.links("http://docs.oracle.com/javase/7/docs/api/")
    options.links("http://developer.android.com/reference/")
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocJar(type: Jar, dependsOn: generateJavadoc) {
    classifier = 'javadoc'
    from generateJavadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        }
    }
    publications {
        mavenAar(MavenPublication) {
            from components.android
            artifact sourceJar
            artifact javadocJar
            artifactId = 'material-intro-screen'

            pom {
                name = 'Material Intro Screen'
                description = 'Provides an intro screen for your app'
                url = 'https://github.com/DreierF/material-intro-screen'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'dreierf'
                        name = 'Florian Dreier'
                    }
                    developer {
                        id = 'tango'
                        name = 'Tango Agency'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/DreierF/material-intro-screen.git'
                    developerConnection = 'scm:git:git://github.com/DreierF/material-intro-screen.git'
                    url = 'https://github.com/DreierF/material-intro-screen'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenAar
}
